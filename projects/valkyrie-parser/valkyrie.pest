program = _{SOI~statement* ~ EOI}

statement = {
    emptyStatement
  | import_statement ~ eos?
  | classStatement ~ eos?
  | traitStatement ~ eos?
  | extendStatement ~ eos?
  | non_local_exit ~ eos?
  | if_statement ~ eos?
  | for_statement ~ eos?
  | while_statement ~ eos?
  | assignStatement ~ eos?
  | defineStatement ~ eos?
  | annotation ~ eos?
  | expression
}
/*====================================================================================================================*/
emptyStatement = {eos}
eos = {Separate|Semicolon}
separator = {eos|Comma}
comma_or_semi = _{Comma|Semicolon}
block_or_stmt = _{block|Set ~ statement}
/*====================================================================================================================*/
import_statement = {
    IMPORT ~ use_alias
  | IMPORT ~ module_block
  | IMPORT ~ use_module_string
  | IMPORT ~ use_module_select
}
use_alias = {use_namepath ~ AS ~ Symbol}
use_module_select = {use_namepath ~ (use_split ~ module_block)?}
use_module_string = {String ~ (use_split? ~ module_block)?}
module_block =  {"{" ~ (module_tuple|comma_or_semi)* ~ "}"}
module_tuple = _{use_alias|use_module_select}
use_split    = _{DOT|Proportion}
use_symbol   = _{use_special|Symbol}
use_namepath = _{use_symbol ~ (use_split ~ use_symbol)*}
//!#C678DD: IMPORT|AS|use_special
IMPORT = @{"using" ~ ("!"|"?"|"*")?}
AS     = @{"as!"|"as?"|"as*"|"as"}
use_special = @{"depot"|"self"|"*"}
/*====================================================================================================================*/
//!#C678DD: WHILE|FOR|IN
for_statement = {
    FOR ~ Symbol ~ IN ~ expr_inline ~ block ~ else_block?
}
while_statement = {
    WHILE ~ block ~ else_block?
  | WHILE ~ expr_inline? ~ block ~ else_block?
}
WHILE = @{"while"}
FOR = @{"for"}
IN = @{"in"}
/*====================================================================================================================*/
//!#C678DD: EXIT
non_local_exit = {EXIT ~ expr}
EXIT = @{"return"|"resume"}
//!#C678DD: Return|Yield|Break|Pass
/*====================================================================================================================*/
block = !{"{" ~ statement* ~ "}"}
/*====================================================================================================================*/
if_statement  = {if_block ~ else_if_block* ~ else_block?}
if_block      = {If ~ condition ~ block}
else_if_block = {Else ~ If ~ condition ~ block}
else_block    = {Else ~ block}
//!#C678DD: If|Else
condition = _{expr_inline}
If        = @{"if"}
Else      = @{"else"}
/*====================================================================================================================*/
match_statement = {
    MATCH ~ expression ~ "{" ~ case_statement+ ~ "}"
}
case_statement = {
    Colon ~ statement
}
MATCH = @{"match"|"catch"}
Case = @{"case"}
/*====================================================================================================================*/
///#C678DD
Type = @{"type"}
/*====================================================================================================================*/
forall_statement = {FORALL ~ (expr ~ Colon ~ expr ~ eos?)+}
FORALL = @{"forall"|"∀"}
/*====================================================================================================================*/
classStatement = {CLASS ~ assign_pair ~ class_block?}
traitStatement = {TRAIT ~ assign_pair ~ class_block?}

class_block = {"{" ~ short_statement* ~ "}"}
short_statement = {
    emptyStatement
  | DEFINE? ~ define_terms ~ eos?
    // mut a = 1 ;
  | assign_pair ~ separator?
  | short_annotation ~ eos?
}
short_annotation = {annotation_call+~short_statement}
CLASS = @{"class"}
TRAIT = @{"trait"}
/*====================================================================================================================*/
extendStatement = {
    EXTENDS ~ Symbol ~ parametric_types? ~ with_trait? ~ class_block
}
with_trait = {"with" ~ Symbol ~ parametric_types?}
EXTENDS = @{"extends"}
/*====================================================================================================================*/
assignStatement = {
    // let (mut a: int, ) = 1;
    LET ~ "(" ~ assign_name ~ ")" ~ type_hint? ~ Set ~ expr ~ eos?
    // let (a: int, b: int)
    // let (a, b): (int, int)
  | LET ~ "(" ~ assign_name ~ ")" ~ type_hint? ~ eos?
    // let mut a: int, = 1;
  | LET ~ assign_name ~ Set ~ expr ~ eos?
    // let a, b: int;
  | LET ~ assign_name ~ eos
  | LET ~ short_statement ~ eos?
}
assign_name = _{assign_pair ~ (Comma ~ assign_pair)* ~ Comma?}
assign_pair = {MODIFIERS* ~ Symbol ~ type_hint?}

///#C678DD
LET = @{"let"}
/*====================================================================================================================*/
defineStatement = {DEFINE}
define_terms = _{
    MODIFIERS* ~ freepath
}
parametric_types = {"[" ~ parametric_types_pair ~ (Comma ~ parametric_types_pair)* ~ "]"}
parametric_types_pair = {(ADD|SUB)? ~ Symbol}

define_parameter = {
    "(" ~ define_pair ~ (Comma ~ define_pair)* ~ Comma? ~ ")"
  | "(" ~ ")"
}
define_pair = {Symbol ~ type_hint? ~ (Set ~ expr)?}
return_type = {(Colon|To) ~ type_expr}
//!#C678DD: DEFINE|WHERE
DEFINE = @{"def"}
/*====================================================================================================================*/
//!Orange:annotation_call
annotation      = {annotation_call+~statement}
annotation_call = @{Sharp~(apply|Symbol)}
macro_call      = @{At ~(apply|Symbol) }
Sharp =@{"#"}
At    =@{"@"}
/*====================================================================================================================*/
expression = {expr ~ eos?}
expr       = {term ~ (Infix ~ term)*}
term       = {Prefix* ~ node ~ Suffix*}
node       = {
    "(" ~ expr ~ ")"
  | data ~ call_once? ~ (callable+ ~ call_once?)+
  | data
}
node_inline = {
    "(" ~ expr ~ ")"
  | data ~ callable*
}
expr_inline = {term_inline ~ (Infix ~ term_inline)*}
term_inline = {Prefix* ~ node_inline ~ Suffix*}
callable    = ${
    DOT ~ COMMENT* ~ dot_call
  | Proportion ~ COMMENT* ~ static_call
  | slice
  | apply
}
call_once   = _{block}
dot_call    = !{namepath|Integer|block|match_call}
static_call = !{Symbol|generic_type}
match_call  = !{MATCH ~ "{" ~ case_statement+ ~ "}"}
/*====================================================================================================================*/
apply       = !{"(" ~ ")" | "(" ~ apply_item ~ (Comma ~ apply_item)* ~ Comma? ~")"}
apply_item  =  {Symbol ~ Colon ~ expr | expr}
/*====================================================================================================================*/
slice       = !{"[" ~ index ~ (Comma ~ index)* ~ Comma? ~ "]"}
index       =  {index_steps|index_range|expr}
index_range = _{index_start? ~ Colon ~ index_end?}
index_steps = _{index_start? ~ Colon ~ index_end? ~ Colon ~ index_step?}
index_start =  {expr}
index_end   =  {expr}
index_step  =  {expr}
/*====================================================================================================================*/
//!#E06C75: type_hint|generic_type|parametric_types_pair
type_expr = _{term ~ (TypeInfix ~ term)*}
//type_term = {Prefix* ~ type_node ~ Suffix*}
//type_node = {"(" ~ type_expr ~ ")"|data}
type_hint = {Colon ~ type_expr}
generic_type = {"[" ~ expr ~ (Comma ~ expr)* ~ Comma? ~ "]"}
//TypePrefix = @{Plus|Minus}
//TypeSuffix = @{Question}
TypeInfix = @{Or}
/*====================================================================================================================*/
data        =  {tuple|table|block|Special|Byte|Complex|Decimal|Integer|String|namepath}
tuple       =  {"(" ~ ")" | "(" ~ expr ~ (Comma ~ expr)* ~ Comma? ~ ")"}
table       =  {"[" ~ "]" | "[" ~ table_item? ~ (Comma ~ table_item)* ~ Comma? ~ "]"}
table_item  = _{table_pair|expr}
table_pair  =  {valid_key ~ Colon ~ expr}
valid_key   = _{Integer | Symbol | String}
/*====================================================================================================================*/
//!#D19A66: Integer|Decimal|DecimalBad|Byte
//!#D19A66: Special
Byte    = ${Zero ~ ASCII_ALPHA ~ (Underline|SUB|ASCII_ALPHANUMERIC)+}
Decimal = ${Integer ~ DOT ~ Integer+}
// DecimalBad = ${Integer ~ Dot|Dot ~ Integer+}
Integer = @{Zero|ASCII_NONZERO_DIGIT ~ (Underline|ASCII_DIGIT)*}
Complex = ${(Decimal|Integer) ~ Symbol}
Zero    = _{"0"}
Special = !{"true" | "false"}
Underline = _{"_"}
/*====================================================================================================================*/
// https://en.wikipedia.org/wiki/Quotation_mark#Summary_table
///#98C379
String         = ${
    Symbol? ~ PUSH(Quotation{3,}|Apostrophe{3,}|Quotation|Apostrophe) ~ (StringItem|!PEEK ~ any)* ~ POP |
    Symbol? ~ "『" ~ (StringItem|!"』" ~ any)* ~ "』" |
    Symbol? ~ "「" ~ (StringItem|!"」" ~ any)* ~ "」" |
    Symbol? ~ "«" ~ (StringItem|!"»" ~ any)* ~ "»" |
    Symbol? ~ "‹" ~ (StringItem|!"›" ~ any)* ~ "›"
}
StringItem     = _{
    "$" ~ Integer           | // $1, $a
    "$" ~ namepath          | // $1, $a
    "${" ~ expression ~ "}" | // ${a}, ${1 + 1}
    StringUnicode           | // \u{20}
    StringEscape
}
StringEscape  =@{"\\" ~ any}
StringUnicode = @{"\\u" ~ "{" ~ASCII_ALPHANUMERIC* ~ "}"}
XmlEscaped    = @{AND ~ ASCII_ALPHANUMERIC+ ~ Semicolon}
Quotation     = _{"\""}
Apostrophe    = _{"'"}
any           = @{ANY}
/*====================================================================================================================*/
//!#61AFEF: SYMBOL_XID
//!#56B6C2: SYMBOL_ESCAPE
Symbol   = ${SYMBOL_XID|SYMBOL_ESCAPE}
namepath = ${Symbol ~ (Proportion ~ Symbol)*}
freepath = ${Symbol ~ ((Proportion|DOT) ~ Symbol)*}
SYMBOL_XID = @{(XID_START|Underline) ~  XID_CONTINUE*}
SYMBOL_ESCAPE = @{Quote ~ (!Quote ~ ANY)+ ~ Quote}
Quote =@{"`"}
/*====================================================================================================================*/
//#7F848E
WHITESPACE = _{NEWLINE|WHITE_SPACE}
COMMENT    =  {CommentN|Comment1}
Comment1   = @{Tilde ~ (!NEWLINE ~ ANY)* }
CommentN   = @{PUSH(Tilde{3,}) ~ (!(Tilde ~ PEEK) ~ ANY)* ~ POP}
Tilde      = @{"~"}
/*====================================================================================================================*/
Proportion = _{"::"|"∷"} //U+2237
Comma      = _{","} //U+FF0C
Colon      = _{":"} //U+FF1A
Semicolon  = _{";"}
Separate   = _{";;"}
//!Orange: Modifier
// Keywords = !{If|For}
MODIFIERS = @{(!(Symbol~(DOT|Set|Comma|Colon|Semicolon|"{"|"}"|"("|")"|"<"|">")) ~ Symbol)}
//!#56B6C2: Prefix|Infix|Postfix
Prefix = @{
    (BANG|ADD|SUB|Star)
}
//!#56B6C2: BANG|QUESTION
Suffix   = @{BANG|QUESTION}
BANG     = @{"!"}
QUESTION = @{"?"}

Infix = _{
    Set
  | (CONCAT|REMOVE)
  | (LOGIC_AND|LOGIC_OR)
  | (ADD|SUB)
  | (Multiply|CenterDot|Kronecker|TensorProduct|Divide|Quotient|Modulo)
  | Power
  | (Grater|GraterEqual|Equal)
  //| Dot
}

CONCAT = @{"++"}
REMOVE = @{"--"}

ADD = @{"+"}
SUB = @{"-"}

AND = @{"&"}
Or  = @{"|"}

LOGIC_AND = @{"||"|"∧"}
LOGIC_OR  = @{"&&"|"∨"}


Star =@{"*"}
Slash =@{"/"}
Solidus = @{"\\"}

DOT = @{"."}


Load =@{"<<<"|"⋘"} //U+22D8
Save =@{">>>"|"⋙"} //U+22D9
LeftShift =@{"<<"|"≪"} //U+226A
RightShift =@{">>"|"≫"} //U+226B
LessEqual =@{"<="}
GraterEqual =@{">="}
Less =@{"<"}
Grater =@{">"}



Equivalent =@{"==="}
NotEquivalent =@{"=!="}
Equal =@{"=="}
NotEqual =@{"!="|"≠"} //U+2260
///#56B6C2
Set = @{"="}



Multiply =@{Star | "×"} //U+00D7
CenterDot = @{"⋅"} //U+22C5
Kronecker =@{"⊗"} //U+2297
TensorProduct=@{"⊙"} //U+2299
Divide = @{Slash | "÷"} //U+00F7
Quotient = @{"//"}
Modulo = @{"%"}


Power = @{"^"}
Surd = @{"√"} //U+221A





To =@{"->"}
Elvis =@{":?"}



LogicNot =@{"¬"} //U+00AC
LogicXor =@{"⊕"} //U+2295

Ellipsis =@{"..."|"…"} // U+2026

Output =@{"%%"}




